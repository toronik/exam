buildscript {
    ext.kotlin_version = '1.7.10'
    ext.ktor_version = '1.6.8'
    ext.exposed_version = '0.38.2'
    ext.restAssured_version = '5.1.1'
    ext.klogging_version = '2.1.21'
    ext.libVersion = '6.0.0-alpha-4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.18.1"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'io.github.adven27'
version = libVersion

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    group = 'io.github.adven27'
    version = libVersion

    sourceCompatibility = 11
    targetCompatibility = 11
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    test {
        systemProperty "user.timezone", "Europe/Moscow"
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
        testLogging.events('FAILED', 'PASSED')
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

task publishMaven {
    dependsOn ":exam-core:publishToSonatype"
    dependsOn ":exam-db:publishToSonatype"
    dependsOn ":exam-ws:publishToSonatype"
    dependsOn ":exam-mq:publishToSonatype"
    dependsOn ":exam-mq-rabbit:publishToSonatype"
    dependsOn ":exam-mq-kafka:publishToSonatype"
    dependsOn ":exam-mq-ibmmq:publishToSonatype"
    dependsOn ":exam-mq-redis:publishToSonatype"
    dependsOn ":exam-files:publishToSonatype"
    dependsOn ":exam-ui:publishToSonatype"
    dependsOn ":exam-ms:publishToSonatype"
    dependsOn ":exam-nosql:publishToSonatype"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
